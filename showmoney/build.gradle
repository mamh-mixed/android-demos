apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.cardinfolink.yunshouyin"
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 55
        versionName "2.0"
        // 3 options: pro, test, dev
        buildConfigField 'String', 'ENVIRONMENT', '"pro"'
    }

    productFlavors{
        cardinfolink {
            manifestPlaceholders = [UMENG_APPKEY_VALUE: "55ebd98367e58e0f13002f2a", UMENG_CHANNEL_VALUE: "yunshouyin"]
//            comment out below for go live
            applicationId "com.cardinfolink.yunshouyin.test"
            buildConfigField 'String', 'ENVIRONMENT', '"test"'
        }
        yinxun {
            manifestPlaceholders = [UMENG_APPKEY_VALUE: "55ebd98367e58e0f13002f2a", UMENG_CHANNEL_VALUE: "yinxun"]
            //            comment out below for go live
            applicationId "com.cardinfolink.yunshouyin.yinxun.test"
            buildConfigField 'String', 'ENVIRONMENT', '"test"'
        }
        japan{
            applicationId "com.cardinfolink.yunshouyin.jp"
            manifestPlaceholders = [UMENG_APPKEY_VALUE: "563029e367e58ee1d5000617", UMENG_CHANNEL_VALUE: "japan"]
            versionName "0.6"
            versionCode 2
            //            comment out below for go live
            buildConfigField 'String', 'ENVIRONMENT', '"test"'
        }
    }

    sourceSets{
        cardinfolink.java.srcDirs = ['src/cardinfolink/java', 'src/main/java']
        yinxun.java.srcDirs = ['src/cardinfolink/java', 'src/main/java']
        japan.java.srcDirs = ['src/japan/java', 'src/main/java']
    }

    signingConfigs {
        debug {
        }

        release {
            storeFile file("../key.keystore")
            storePassword "cil1009"
            keyAlias "cardinfolink"
            keyPassword "cil1009"
        }
    }

    buildTypes {
        release {
//            TODO: 混淆代码
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            shrinkResources false
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "showmoney_v${variant.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
}

dependencies {
    compile project(':cashierSdk')
    compile project(':library')
    compile project(':menulibrary')
    compile files('libs/commons-codec-1.3.jar')
    compile files('libs/core.jar')
    compile files('libs/umeng-analytics-v5.5.3.jar')
    compile files('libs/umeng-update-v2.6.0.1.jar')
}

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}